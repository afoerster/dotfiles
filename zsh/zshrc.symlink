# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH=/Library/Python/2.7/site-packages/:/usr/local/sbin:/usr/local/bin:/usr/local/opt/:/usr/bin:/bin:/usr/sbin:/sbin:/Users/anthonyfoerster/.scripts:/Users/anthonyfoerster/bin/kafka_2.10-0.8.2.2/bin:/Users/anthonyfoerster/code/rebar/:./bin:/usr/local/bin:/usr/local/sbin:/Users/anthonyfoerster/.dotfiles/bin:/Users/anthonyfoerster/.rbenv/shims:/Users/anthonyfoerster/.yarn/bin:/Users/anthonyfoerster/Code/go/bin:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin:/Library/Python/2.7/site-packages/:/usr/local/sbin:/usr/local/opt/:/Users/anthonyfoerster/.scripts:/Users/anthonyfoerster/bin/kafka_2.10-0.8.2.2/bin:/Users/anthonyfoerster/code/rebar/:/Users/anthonyfoerster/.vimpkg/bin
